//
// Copyright (c) ZeroC, Inc. All rights reserved.
//

//
// Install gradle Ice builder plug-in from the plug-in portal. We use `apply false`
// to tell Gradle not to apply the plugin to the current project. The subprojects
// use `apply plugin` to apply the plugin.
//
plugins {
    id 'com.zeroc.gradle.ice-builder.slice' version '1.4.7' apply false
}

subprojects {
    //
    // Apply Java and Slice plug-ins to all subprojects
    //
    apply plugin: 'java'
    apply plugin: 'com.zeroc.gradle.ice-builder.slice'

    //
    // Both projects Client and Server share the Printer.ice Slice definitions
    //
    slice {
        java {
            files = [file("../Printer.ice")]
        }
    }

    //
    // Use Ice JAR files from maven central repository
    //
    repositories {
        mavenCentral()
    }

    //
    // Both Client and Server depend only on the Ice JAR file
    //
    dependencies {
        implementation "com.zeroc:ice:${slice.iceVersion}"
    }

    //
    // Create a JAR file with the appropiate Main-Class and Class-Path attributes
    //
    jar {
        manifest {
            attributes(
                "Main-Class" : project.name.capitalize(),
                "Class-Path": configurations.runtimeClasspath.resolve().collect { it.toURI() }.join(' ')
            )
        }
    }
}
