//
// Copyright (c) ZeroC, Inc. All rights reserved.
//

['callback', 'simpleChat'].each {
    def name = it
    project(":demoGlacier2${it.capitalize()}") {
        slice {
            java {
                include = ["${projectDir}"]
                srcDir = '.'
            }
        }

        sourceSets {
            main {
                java {
                    srcDir '.'
                }
            }
        }

        dependencies {
            implementation "com.zeroc:ice:${project.iceArtifactVersion}"
            runtimeOnly "com.zeroc:icessl:${project.iceArtifactVersion}"
            implementation "com.zeroc:glacier2:${project.iceArtifactVersion}"
            runtimeOnly "org.apache.commons:commons-compress:1.14"
        }

        // Don't create the default jar.
        jar.enabled = false

        demoJar("client")

        // The simpleChat demo has no serverJar.
        if(name != "simpleChat") {
            demoJar("server")
        }
    }
}
